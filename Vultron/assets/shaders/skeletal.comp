#version 460

struct InstanceData {
    mat4 model;
    ivec4 boneAndInstanceOffsetAndCount;
    int boneOutputOffset;
};

layout(std140, set = 0, binding = 0) readonly buffer InstanceBufferObject {
    InstanceData instances[];
};

struct SkeletonBone {
    mat4 offset;
    int parent;
};

layout(set = 0, binding = 1) uniform SkeletonBoneObject {
    SkeletonBone bones[128];
};

struct AnimationFrame {
    vec3 position;
    vec4 rotation;
    vec3 scale;
};

layout(std140, set = 0, binding = 2) readonly buffer AnimationBufferObject {
    AnimationFrame frames[];
};

struct AnimationInstance {
    ivec4 frameOffsetAnd1And2AndRef;
    vec2 timeAndBlendFactor;
};

layout(set = 0, binding = 3) uniform AnimationInstanceObject {
    AnimationInstance animationInstances[128];
};

layout(std140, set = 0, binding = 4) writeonly buffer BoneOutputBufferObject {
    mat4 boneOutput[];
};

vec3 QMulV(vec4 q, vec3 v) {
    return q.xyz * 2.0f * dot(q.xyz, v) +
           v * (q.w * q.w - dot(q.xyz, q.xyz)) +
           cross(q.xyz, v) * 2.0f * q.w;
}

mat4 ToMatrix(vec3 position, vec4 rotation, vec3 scale) {
    vec3 xBasis = QMulV(rotation, vec3(scale.x, 0, 0));
    vec3 yBasis = QMulV(rotation, vec3(0, scale.y, 0));
    vec3 zBasis = QMulV(rotation, vec3(0, 0, scale.z));

    return mat4(
        xBasis.x, xBasis.y, xBasis.z, 0.0,
        yBasis.x, yBasis.y, yBasis.z, 0.0,
        zBasis.x, zBasis.y, zBasis.z, 0.0,
        position.x, position.y, position.z, 1.0
    );
}

mat4 MixMatrices(mat4 a, mat4 b, float blend) {
    return a * (1.0 - blend) + b * blend;
}

struct BonePose {
    vec3 position;
    vec4 rotation;
    vec3 scale;
};

mat4 GetPose(int animationInstanceOffset, int animationInstanceCount, int boneIndex, int boneOffset, int boneCount)
{
    mat4 boneMatrix = mat4(1.0);
    int currBoneIndex = boneIndex;

    for (int b = 0; b < boneCount; b++)
    { 
        float totalBlendFactor = 0.0;
        BonePose accPose = BonePose(vec3(0.0), vec4(0.0, 0.0, 0.0, 0.0), vec3(0.0));
        
        for (int i = 0; i < animationInstanceCount; i++) {
            int frameOffset = animationInstances[animationInstanceOffset + i].frameOffsetAnd1And2AndRef.x;
            int frame1 = animationInstances[animationInstanceOffset + i].frameOffsetAnd1And2AndRef.y;
            int frame2 = animationInstances[animationInstanceOffset + i].frameOffsetAnd1And2AndRef.z;
            int refFrame = animationInstances[animationInstanceOffset + i].frameOffsetAnd1And2AndRef.w;
            float frameBlend = animationInstances[animationInstanceOffset + i].timeAndBlendFactor.x;
            float blendFactor = animationInstances[animationInstanceOffset + i].timeAndBlendFactor.y;

            if (refFrame != -1 || blendFactor <= 0.0) {
                continue;
            }

            // Blending between two frames
            int frame1Index = frameOffset + frame1 * boneCount + currBoneIndex;
            int frame2Index = frameOffset + frame2 * boneCount + currBoneIndex;

            vec4 rotation1 = normalize(frames[frame1Index].rotation);
            vec4 rotation2 = normalize(frames[frame2Index].rotation);

            if (dot(rotation1, rotation2) < 0.0) { rotation1 *= -1.0; }
            vec3 position = mix(frames[frame1Index].position, frames[frame2Index].position, frameBlend);
            vec4 rotation = normalize(mix(rotation1, rotation2, frameBlend));            
            vec3 scale = mix(frames[frame1Index].scale, frames[frame2Index].scale, frameBlend);

            if(dot(accPose.rotation, rotation) < 0.0) { accPose.rotation *= -1.0; }
            accPose.position += position * blendFactor;
            accPose.rotation += rotation * blendFactor;
            accPose.scale += scale * blendFactor;
            totalBlendFactor += blendFactor;
        }

        accPose.position /= totalBlendFactor;
        accPose.rotation /= totalBlendFactor;
        accPose.scale /= totalBlendFactor;

        accPose.rotation = normalize(accPose.rotation);
        mat4 baseMatrix = ToMatrix(accPose.position, accPose.rotation, accPose.scale);
        
        // Additive blending
        mat4 additiveMatrix = mat4(1.0);
        BonePose addPose = BonePose(vec3(0.0), vec4(0.0, 0.0, 0.0, 0.0), vec3(0.0));
        int refFrameIndex = -1;
        totalBlendFactor = 0.0;

        for (int i = 0; i < animationInstanceCount; i++) {
            int frameOffset = animationInstances[animationInstanceOffset + i].frameOffsetAnd1And2AndRef.x;
            int frame1 = animationInstances[animationInstanceOffset + i].frameOffsetAnd1And2AndRef.y;
            int frame2 = animationInstances[animationInstanceOffset + i].frameOffsetAnd1And2AndRef.z;
            int refFrame = animationInstances[animationInstanceOffset + i].frameOffsetAnd1And2AndRef.w;
            float frameBlend = animationInstances[animationInstanceOffset + i].timeAndBlendFactor.x;
            float blendFactor = animationInstances[animationInstanceOffset + i].timeAndBlendFactor.y;

            if (refFrame == -1 || blendFactor <= 0.0) {
                continue;
            }

            refFrameIndex = refFrame + currBoneIndex;

            // Blending between two frames
            int frame1Index = frameOffset + frame1 * boneCount + currBoneIndex;
            int frame2Index = frameOffset + frame2 * boneCount + currBoneIndex;

            vec4 rotation1 = normalize(frames[frame1Index].rotation);
            vec4 rotation2 = normalize(frames[frame2Index].rotation);

            if (dot(rotation1, rotation2) < 0.0) { rotation1 *= -1.0; }
            vec3 position = mix(frames[frame1Index].position, frames[frame2Index].position, frameBlend);
            vec4 rotation = normalize(mix(rotation1, rotation2, frameBlend));            
            vec3 scale = mix(frames[frame1Index].scale, frames[frame2Index].scale, frameBlend);

            if(dot(addPose.rotation, rotation) < 0.0) { addPose.rotation *= -1.0; }
            addPose.position += position * blendFactor;
            addPose.rotation += rotation * blendFactor;
            addPose.scale += scale * blendFactor;
            totalBlendFactor += blendFactor;
        }

        if (totalBlendFactor > 0.0 && refFrameIndex != -1) {
            addPose.position /= totalBlendFactor;
            addPose.rotation /= totalBlendFactor;
            addPose.scale /= totalBlendFactor;
            addPose.rotation = normalize(addPose.rotation);
            mat4 refMatrix = inverse(ToMatrix(frames[refFrameIndex].position, frames[refFrameIndex].rotation, frames[refFrameIndex].scale));
            additiveMatrix = refMatrix * ToMatrix(addPose.position, addPose.rotation, vec3(1.0));
        }

        boneMatrix = MixMatrices(baseMatrix, baseMatrix * additiveMatrix, 1.0) * boneMatrix;
        currBoneIndex = bones[boneOffset + currBoneIndex].parent;

        if (currBoneIndex == -1)
        {
            break;
        }
    }

    return boneMatrix;
}

layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

void main()  {
    int instanceIndex = int(gl_WorkGroupID.x);
    int boneIndex = int(gl_LocalInvocationID.x);
    int boneCount = int(min(128, instances[instanceIndex].boneAndInstanceOffsetAndCount.y));

    if (boneIndex < boneCount) {
        int boneOffset = instances[instanceIndex].boneAndInstanceOffsetAndCount.x;
        int animationInstanceOffset = instances[instanceIndex].boneAndInstanceOffsetAndCount.z;
        int animationInstanceCount = instances[instanceIndex].boneAndInstanceOffsetAndCount.w;
        int boneOutputOffset = instances[instanceIndex].boneOutputOffset;

        mat4 offset = bones[boneOffset + boneIndex].offset;
        boneOutput[boneOutputOffset + boneIndex] = GetPose(animationInstanceOffset, animationInstanceCount, boneIndex, boneOffset, boneCount) * offset;
    }
}